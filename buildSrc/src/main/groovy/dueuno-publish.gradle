plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = projectGroup
version = projectVersion

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForMavenJavaPublication }
        rename '.*', 'pom.xml'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java
            pom {
                name = project.name
                url = 'https://dueuno.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'gianluca.sartori'
                        name = 'Gianluca Sartori'
                        email = 'g.sartori@gmail.com'
                    }
                    developer {
                        id = 'francesco.piceghello'
                        name = 'Francesco Piceghello'
                        email = 'f.piceghello@gmail.com'
                    }
                }
            }
        }
    }

    Boolean isSnapshot = version.endsWith('SNAPSHOT')
    String repositoryUrl = isSnapshot ? repositorySnapshot : repositoryRelease
    repositories {
        maven {
            name 'OSSRH'
            url repositoryUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
