/*
 * Copyright 2021 the original author or authors.
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 *
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package dueuno.elements.components

import dueuno.commons.utils.LogUtils
import dueuno.core.LinkGeneratorAware
import dueuno.core.WebRequestAware
import dueuno.elements.core.ApplicationService
import dueuno.elements.core.PageService
import dueuno.elements.core.SystemPropertyService
import dueuno.elements.pages.Shell
import dueuno.elements.pages.ShellConfig
import dueuno.elements.tenants.TenantPropertyService
import dueuno.elements.tenants.TenantService
import dueuno.exceptions.ElementsException
import groovy.transform.CompileStatic
import groovy.util.logging.Slf4j

/**
 * @author Gianluca Sartori
 */

@Slf4j
@CompileStatic
class ShellService implements WebRequestAware, LinkGeneratorAware {

    ApplicationService applicationService
    TenantService tenantService
    SystemPropertyService systemPropertyService
    TenantPropertyService tenantPropertyService
    PageService pageService

    void tenantInstall() {
        String tenantId = tenantService.currentTenantId
        tenantPropertyService.setString('LOGO', linkPublicResource(tenantId, '/brand/logo.png', false))
    }

    Shell getShell() {
        Shell shell = pageService.mainPage as Shell
        return shell ?: createShell()
    }

    private Shell createShell() {
        try {
            ShellConfig config = readConfig()
            Map shellArgs = [config: config]
            Shell newShell = pageService.createPage(Shell, shellArgs)
            pageService.mainPage = newShell
            return newShell

        } catch (Exception e) {
            log.error LogUtils.logStackTrace(e)
            throw new ElementsException("Cannot create the shell: ${e.message}", e)
        }
    }

    private ShellConfig readConfig() {
        ShellConfig shellConfig = new ShellConfig()

        // These are not cloned, we will have direct access to the servletContext objects
        shellConfig.features.user = applicationService.userFeatures
        shellConfig.features.main = applicationService.mainFeatures

        // System properties
        shellConfig.display.menu = systemPropertyService.getBoolean('DISPLAY_MENU', true)
        shellConfig.display.menuSearch = systemPropertyService.getBoolean('DISPLAY_MENU_SEARCH', true)
        shellConfig.display.homeButton = systemPropertyService.getBoolean('DISPLAY_HOME_BUTTON', true)
        shellConfig.display.userMenu = systemPropertyService.getBoolean('DISPLAY_USER_MENU', true)

        // Tenant properties
        shellConfig.display.logo = tenantPropertyService.getString('LOGO', true)

        return shellConfig
    }
}
